; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; Requires Inno Setup Preprocessor by Alex Yackimoff
#pragma option -v+
#pragma verboselevel 9

#define UDMPATH GetEnv('UDM_PATH')
#if UDMPATH == ""
#define UDMPATH "E:\MoBIES\UDM"
#endif
#define UDMPATH_VS2005 "E:\MoBIES\UDM"
#define UDMPATH_VS2008 "E:\MoBIES\UDM_VS2008"


#define UDM3rdPartyPATH_VS2005 "E:\MoBIES\udm3rdparty"
#define UDM3rdPartyPATH_VS2008 "E:\MoBIES\udm3rdparty_vs2008"

#define GREATPATH_VS2005 "E:\MoBIES\GReAT"
#define GREATPATH_VS2008 "E:\MoBIES\GReAT_VS2008"

#define UDMDLL "UdmDll_3_1_3.dll"
#define UDMDLLD "UdmDll_3_1_3D.dll"
#define UDMVER "3.1.3"
#define GMEVER "7.6.29"
#define OutputFileBase "Udm_setup"

[Setup]
AppName=UDM {#UDMVER}
APPVerName=UDM
AppVersion={#UDMVER}
AppPublisher=ISIS, Vanderbilt University
AppPublisherURL=http://www.escherinstitute.org/Plone/tools/suites/mic/udm
AppSupportURL=http://www.escherinstitute.org/Plone/tools/suites/mic/udm
AppUpdatesURL=http://www.escherinstitute.org/Plone/tools/suites/mic/udm
DefaultDirName={pf}\ISIS\UDM
DefaultGroupName=UDM
LicenseFile={#UDMPATH}\license.txt
InfoBeforeFile={#UDMPATH}\releaseinfo.txt
OutputDir={#UDMPATH}\InnoSetup
OutputBaseFilename={#OutputFileBase}
Compression=lzma
SolidCompression=yes
ChangesEnvironment=true
AppCopyright=Copyright (C) 2000-2006 ISIS, Vanderbilt University

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Types]
Name: Full; Description: Full(C++, Java and Full 3rdParty Package); Flags: iscustom
Name: Typical; Description: Typical(C++)

[Components]
Name: Core; Description: Core UDM; Types: Typical Full; Flags: fixed
Name: C; Description: C++ UDM; Types: Typical Full; Flags: fixed
Name: Java; Description: Java UDM; Types: Full; Flags: fixed
Name: System; Description: Register Environment Variables System-Wide (recommended); Types: Typical Full
Name: Full3rdParty; Description: Full 3rdParty Package; Types: Full

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
;Udm Root folder
Source: {#UDMPATH}\readme.txt; DestDir: {app}; Components: Core; Flags: ignoreversion; DestName: readme.txt

; Udm_3rdparty_files
Source: {#UDM3rdPartyPath_VS2005}\antlr-2.7.1\*; DestDir: {app}\3rdparty\antlr-2.7.1; Excludes: *.scc; Components: Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs
Source: {#UDM3rdPartyPath_VS2005}\ant-1.6.2\*; DestDir: {app}\3rdparty\ant-1.6.2; Excludes: *.scc; Components: Java Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs
Source: {#UDM3rdPartyPath_VS2005}\boost_1_34_1\*; DestDir: {app}\3rdparty\boost_1_34_1; Excludes: *.scc; Components: Java Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs
Source: {#UDM3rdPartyPath_VS2005}\cppunit-1.10.2\*; DestDir: {app}\3rdparty\cppunit-1.10.2; Excludes: *.scc; Components: Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs
Source: {#UDM3rdPartyPath_VS2005}\junit\*; DestDir: {app}\3rdparty\junit; Excludes: *.scc; Components: Java Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs
Source: {#UDM3rdPartyPath_VS2005}\loki-0.1.6\*; DestDir: {app}\3rdparty\loki-0.1.6; Excludes: *.scc; Components: Java Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs
Source: {#UDM3rdPartyPath_VS2005}\mpc\*; DestDir: {app}\3rdparty\mpc; Excludes: clone_build_tree.exe, mnwc.exe, prj_install.exe, *.scc; Components: C Full3rdParty; Flags: ignoreversion
Source: {#UDM3rdPartyPath_VS2005}\mpc\config\*; DestDir: {app}\3rdparty\mpc\config; Excludes: *.scc; Components: C Full3rdParty; Flags: ignoreversion
Source: {#UDM3rdPartyPath_VS2005}\mpc\devtools\*; DestDir: {app}\3rdparty\mpc\devtools; Excludes: *.scc; Components: C Full3rdParty; Flags: ignoreversion
Source: {#UDM3rdPartyPath_VS2005}\mpc\docs\*; DestDir: {app}\3rdparty\mpc\docs; Excludes: *.scc; Components: C Full3rdParty; Flags: ignoreversion recursesubdirs
Source: {#UDM3rdPartyPath_VS2005}\mpc\history\*; DestDir: {app}\3rdparty\mpc\history; Excludes: *.scc; Components: C Full3rdParty; Flags: ignoreversion
Source: {#UDM3rdPartyPath_VS2005}\mpc\modules\*; DestDir: {app}\3rdparty\mpc\modules; Excludes: *.scc; Components: C Full3rdParty; Flags: ignoreversion recursesubdirs
Source: {#UDM3rdPartyPath_VS2005}\mpc\templates\*; DestDir: {app}\3rdparty\mpc\templates; Excludes: *.scc; Components: C Full3rdParty; Flags: ignoreversion
Source: {#UDM3rdPartyPath_VS2005}\zlib\license.txt; DestDir: {app}\3rdparty\zlib; Components: Core; Flags: ignoreversion
Source: {#UDM3rdPartyPath_VS2005}\zlib\zlib.lib; DestDir: {app}\3rdparty\zlib; Components: Core; Flags: ignoreversion
Source: {#UDM3rdPartyPath_VS2005}\zlib\zlibD.lib; DestDir: {app}\3rdparty\zlib; Components: Core; Flags: ignoreversion
Source: {#UDM3rdPartyPath_VS2005}\xerces-c_2_5_0\license.txt; DestDir: {app}\3rdparty\xerces-c_2_5_0; Components: Core; Flags: ignoreversion
;VS2005
Source: {#UDM3rdPartyPath_VS2005}\xalan-c_1_8_0\license.txt; DestDir: {app}\3rdparty\xalan-c_1_8_0; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDM3rdPartyPath_VS2005}\xalan-c_1_8_0\bin\*; DestDir: {app}\3rdparty\xalan-c_1_8_0\bin; Excludes: *.scc; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDM3rdPartyPath_VS2005}\xalan-c_1_8_0\lib\*; DestDir: {app}\3rdparty\xalan-c_1_8_0\lib; Excludes: *.scc; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDM3rdPartyPath_VS2005}\xalan-c_1_8_0\include\*; DestDir: {app}\3rdparty\xalan-c_1_8_0\include; Excludes: *.scc; Components: Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs; Check: VS2005Check
Source: {#UDM3rdPartyPath_VS2005}\xerces-c_2_5_0\bin\*; DestDir: {app}\3rdparty\xerces-c_2_5_0\bin; Excludes: *.scc; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDM3rdPartyPath_VS2005}\xerces-c_2_5_0\lib\*; DestDir: {app}\3rdparty\xerces-c_2_5_0\lib; Excludes: *.scc; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDM3rdPartyPath_VS2005}\xerces-c_2_5_0\include\*; DestDir: {app}\3rdparty\xerces-c_2_5_0\include; Excludes: *.scc; Components: Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs; Check: VS2005Check
;VS2008
Source: {#UDM3rdPartyPath_VS2008}\xalan-c_1_11_0\bin\*; DestDir: {app}\3rdparty\xalan-c_1_11_0\bin; Excludes: *.scc; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDM3rdPartyPath_VS2008}\xalan-c_1_11_0\lib\*; DestDir: {app}\3rdparty\xalan-c_1_11_0\lib; Excludes: *.scc; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDM3rdPartyPath_VS2008}\xalan-c_1_11_0\include\*; DestDir: {app}\3rdparty\xalan-c_1_11_0\include; Excludes: *.scc; Components: Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs; Check: VS2008Check
Source: {#UDM3rdPartyPath_VS2008}\xerces-c_2_5_0\bin\*; DestDir: {app}\3rdparty\xerces-c_2_5_0\bin; Excludes: *.scc; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDM3rdPartyPath_VS2008}\xerces-c_2_5_0\lib\*; DestDir: {app}\3rdparty\xerces-c_2_5_0\lib; Excludes: *.scc; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDM3rdPartyPath_VS2008}\xerces-c_2_5_0\include\*; DestDir: {app}\3rdparty\xerces-c_2_5_0\include; Excludes: *.scc; Components: Full3rdParty; Flags: ignoreversion recursesubdirs createallsubdirs; Check: VS2008Check

;Udm bin folder
Source: {#UDMPATH_VS2005}\bin\judm.jar; DestDir: {app}\bin; Components: Java; Flags: ignoreversion
;VS2005
Source: {#UDM3rdPartyPath_VS2005}\xerces-c_2_5_0\bin\xerces-c_2_5_0.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDM3rdPartyPath_VS2005}\xalan-c_1_8_0\bin\Xalan-C_1_8.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDM3rdPartyPath_VS2005}\xalan-c_1_8_0\bin\XalanMessages_1_8.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\{#UDMDLL}; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\{#UDMDLLD}; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\GmeOclExprChecker.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion regserver; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\UML2XML.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion regserver; Check: VS2005Check
Source: {#GREATPATH_VS2005}\bin\MetaGME2Uml.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion regserver; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\Udm.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\UdmBackendDump.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\UdmCopy.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\UdmOclPat.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\UdmToXmi.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\UdmViz.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\UIntWiz.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\XmiToUdm.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\bin\UdmSwig.dll; DestDir: {app}\bin; Components: Java; Flags: ignoreversion; Check: VS2005Check
;VS2008
Source: {#UDM3rdPartyPath_VS2008}\xerces-c_2_5_0\bin\xerces-c_2_5_0.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDM3rdPartyPath_VS2008}\xalan-c_1_11_0\bin\Xalan-C_1_11.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDM3rdPartyPath_VS2008}\xalan-c_1_11_0\bin\XalanMessages_1_11.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\{#UDMDLL}; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\{#UDMDLLD}; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\GmeOclExprChecker.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion regserver; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\UML2XML.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion regserver; Check: VS2008Check
Source: {#GREATPATH_VS2008}\bin\MetaGME2Uml.dll; DestDir: {app}\bin; Components: Core; Flags: ignoreversion regserver; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\Udm.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\UdmBackendDump.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\UdmCopy.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\UdmOclPat.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\UdmToXmi.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\UdmViz.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\UIntWiz.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\XmiToUdm.exe; DestDir: {app}\bin; Components: Core; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\bin\UdmSwig.dll; DestDir: {app}\bin; Components: Java; Flags: ignoreversion; Check: VS2008Check

;Udm Doc folder
Source: {#UDMPATH_VS2005}\doc\UDMAPI.pdf; DestDir: {app}\doc; Flags: ignoreversion; Components: Core
Source: {#UDMPATH_VS2005}\doc\UdmInterpreterWizard.pdf; DestDir: {app}\doc; Flags: ignoreversion; Components: Core
Source: {#UDMPATH_VS2005}\judm\build\build_win\judm\apidoc\*; DestDir: {app}\doc\apidoc; Excludes: *.scc; Flags: recursesubdirs createallsubdirs ignoreversion; Components: Java

;Udm etc folder
Source: {#UDMPATH_VS2005}\etc\CreateUdmXml.vbs; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\compileUdmAPI.bat; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\CreateMGA.vbs; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\CreateXmp.vbs; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\Uml.xsd; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\UdmProject.xme; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\UdmProject_udm.xml; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\UdmProject.xsd; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\Uml.xml; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\Xme2UdmXml.bat; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\UmlInUml.xme; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\UmlMeta.xme; DestDir: {app}\etc; Components: Core; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\JAVA\judmAPI.bat; DestDir: {app}\etc\Java; Components: Java; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\JAVA\.classpath; DestDir: {app}\etc\Java; Components: Java; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\JAVA\.project; DestDir: {app}\etc\Java; Components: Java; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\JAVA\build.xml; DestDir: {app}\etc\Java; Components: Java; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\etc\JAVA\eclipseAPI.bat; DestDir: {app}\etc\Java; Components: Java; Flags: ignoreversion
;VS2005
Source: {#UDMPATH_VS2005}\etc\MPCGen.exe; DestDir: {app}\etc; Components: C; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\etc\StrResGen.exe; DestDir: {app}\etc; Components: C; Flags: ignoreversion; Check: VS2005Check
;VS2008
Source: {#UDMPATH_VS2008}\etc\MPCGen.exe; DestDir: {app}\etc; Components: C; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\etc\StrResGen.exe; DestDir: {app}\etc; Components: C; Flags: ignoreversion; Check: VS2008Check

;Udm include folder
Source: {#UDMPATH_VS2005}\include\UdmBase.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\Uml_export.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UdmXmi.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\cint_string.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UdmUtil.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\ErrHand.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UdmGme.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UdmProject.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UdmDom.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UdmOcl.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UdmCintSwig.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UdmStatic.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UmlExt.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\Uml.h; DestDir: {app}\include; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\include\UdmMPL.h; DestDir: {app}\include; Flags: ignoreversion

;Udm lib folder
;VS2005
Source: {#UDMPATH_VS2005}\lib\GOCLD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmDll.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmDllD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmXmi.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmXmiD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\File2Code.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\File2CodeD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmUtil.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\Uml.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UmlD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmDom.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmUtilD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmPOI.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\JavaAPIGen.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmPOID.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\JavaAPIGenD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmOcl.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmGme.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmDomD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\GOCL.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmGmeD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmOclD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmBase.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmBaseD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\lib\UdmDllD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2005Check
;VS2008
Source: {#UDMPATH_VS2008}\lib\GOCLD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmDll.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmDllD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmXmi.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmXmiD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\File2Code.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\File2CodeD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmUtil.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\Uml.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UmlD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmDom.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmUtilD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmPOI.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\JavaAPIGen.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmPOID.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\JavaAPIGenD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmOcl.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmGme.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmDomD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\GOCL.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmGmeD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmOclD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmBase.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmBaseD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2008}\lib\UdmDllD.lib; DestDir: {app}\lib; Flags: ignoreversion; Check: VS2008Check

;Udm Samples folder
Source: {#UDMPATH_VS2005}\samples\LampDiagramGME.xme; DestDir: {app}\samples\C++; Components: C; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\samples\LampDiagramUML.xme; DestDir: {app}\samples\C++; Components: C; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\samples\CreateLampModel\CreateLampModel.rc; DestDir: {app}\samples\C++\CreateLampModel; Components: C; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\samples\CreateLampModel\CreateLampModelVS2005.vcproj; DestDir: {app}\samples\C++\CreateLampModel; Components: C; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\samples\CreateLampModel\CreateLampModelVS2008.vcproj; DestDir: {app}\samples\C++\CreateLampModel; Components: C; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2005}\samples\CreateLampModel\CreateLampModel.cpp; DestDir: {app}\samples\C++\CreateLampModel; Components: C; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\samples\cross_links\cross_links.cpp; DestDir: {app}\samples\C++\cross_links; Components: C; Flags: ignoreversion
Source: {#UDMPATH_VS2005}\samples\cross_links\cross_linksVS2005.vcproj; DestDir: {app}\samples\C++\cross_links; Components: C; Flags: ignoreversion; Check: VS2005Check
Source: {#UDMPATH_VS2005}\samples\cross_links\cross_linksVS2008.vcproj; DestDir: {app}\samples\C++\cross_links; Components: C; Flags: ignoreversion; Check: VS2008Check
Source: {#UDMPATH_VS2005}\samples\cross_links\CL.xme; DestDir: {app}\samples\C++\cross_links; Components: C; Flags: ignoreversion

[Dirs]
Name: {app}\include; Components: C
Name: {app}\lib; Components: C

[Messages]
SelectComponentsLabel2=Select the setup type that best suits your needs. Click Next when you are ready to continue.

[Registry]
Root: HKCU; Subkey: Environment; ValueType: string; ValueName: UDM_PATH; ValueData: {app}; Flags: uninsdeletevalue deletevalue; Components: not System
Root: HKCU; Subkey: Environment; ValueType: string; ValueName: UDM_3RDPARTY_PATH; ValueData: {app}\3rdparty; Flags: uninsdeletevalue deletevalue; Components: not System
Root: HKLM; Subkey: SYSTEM\CurrentControlSet\Control\Session Manager\Environment; ValueType: string; ValueName: UDM_PATH; ValueData: {app}; Flags: uninsdeletevalue deletevalue; Components: System
Root: HKLM; Subkey: SYSTEM\CurrentControlSet\Control\Session Manager\Environment; ValueType: string; ValueName: UDM_3RDPARTY_PATH; ValueData: {app}\3rdparty; Flags: uninsdeletevalue deletevalue; Components: System

[Code]
var
  InstallSystemWide: Boolean;
  InstallVS2005: Boolean;
  InstallVS2008: Boolean;
  VSOptionPage: TInputOptionWizardPage;

function InitializeSetup(): Boolean;
var
  GME_Path : String;
  oGME :  Variant;
  vs_path : String;
begin
	GME_Path := GetEnv('GME_ROOT');
	if GME_Path = '' then begin
		MsgBox('Unable to locate GME r{#GMEVER}.  Please install GME r{#GMEVER} first.', mbError, MB_OK);
		Result := False;
	end else begin
		oGME := CreateOleObject('GME.Application');
		if oGME.Version = '{#GMEVER}' then begin
			Result := True;
		end else begin
			MsgBox('Udm {#UDMVER} needs GME r{#GMEVER}. The current GME version is '+oGME.Version+'.'#13#13'Please install GME r{#GMEVER} first.', mbError, MB_OK);
			Result := False;
		end;
	end;
	if Result = True then begin
		RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\MicroSoft\VisualStudio\9.0', 'InstallDir', vs_path);
		if vs_path = '' then begin
			InstallVS2008 := False;
		end else begin;
			InstallVS2008 := True;
		end;
		if InstallVS2008 = False then begin
			RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\MicroSoft\VisualStudio\8.0', 'InstallDir', vs_path);
			if vs_path = '' then begin
				InstallVS2005 := False;
			end else begin
				InstallVS2005 := True;
			end;
		end;
	end;
end;

function VS2005Check(): Boolean;
begin
	Result := InstallVS2005;
end;

function VS2008Check(): Boolean;
begin
	Result := InstallVS2008;
end;

function InitializeUninstall(): Boolean;
begin
  //Need to know if this was a system wide install or not
  InstallSystemWide := RegValueExists(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'UDM_PATH');
  Result := True;
end;

Procedure ModPath(const ValueName, pathdir: String);
var
	oldpath:	String;
	newpath:	String;
	pathArr:	TArrayOfString;
	i:			Integer;
begin
	// Modify WinNT path
	if UsingWinNT() = true then begin

		// Get current path, split into an array
		if InstallSystemWide then begin
		  RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', ValueName, oldpath);
		end else begin
		  RegQueryStringValue(HKEY_CURRENT_USER, 'Environment', ValueName, oldpath);
    end;
		if oldpath <> '' then
			oldpath := oldpath + ';';
		i := 0;
		while (Pos(';', oldpath) > 0) do begin
			SetArrayLength(pathArr, i+1);
			pathArr[i] := Copy(oldpath, 0, Pos(';', oldpath)-1);
			oldpath := Copy(oldpath, Pos(';', oldpath)+1, Length(oldpath));
			i := i + 1;

			// Check if current directory matches app dir
			if pathdir = pathArr[i-1] then begin
				// if uninstalling, remove dir from path
				if IsUninstaller() = true then begin
					continue;
				// if installing, abort because dir was already in path
				end else begin
					exit;
				end;
			end;

			// Add current directory to new path
			if i = 1 then begin
				newpath := pathArr[i-1];
			end else begin
				newpath := newpath + ';' + pathArr[i-1];
			end;
		end;

		if IsUninstaller() = false then begin
			if newpath <> '' then begin
				newpath := newpath + ';' + pathdir;
			end else begin
				newpath := pathdir;
			end;
		end;

		// Write new path
		if InstallSystemWide then begin
		  RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', ValueName, newpath);
		end else begin
		  RegWriteStringValue(HKEY_CURRENT_USER, 'Environment', ValueName, newpath);
    end;
	end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
	path : String;
begin
	if CurStep = ssInstall then begin
		if (InstallVS2008 = False) and (InstallVS2005 = False) then begin
			case VSOptionPage.SelectedValueIndex of
				0: InstallVS2005 := True;
				1: InstallVS2008 := True;
			end;
		end;

		path := ExpandConstant('{app}');
//		RegWriteStringValue(HKEY_CURRENT_USER, 'Environment', 'UDM_PATH', path);
//		RegWriteStringValue(HKEY_CURRENT_USER, 'Environment', 'UDM_3RDPARTY_PATH', path+'3rdparty');
		ModPath('PATH', path+'\bin');
		ModPath('PATH', path+'\etc');
		ModPath('PATH', path+'\3rdparty\xerces-c_2_5_0\bin');
		if InstallVS2005 then begin
			ModPath('PATH', path+'\3rdparty\xalan-c_1_8_0\bin');
		end else begin
			ModPath('PATH', path+'\3rdparty\xalan-c_1_11_0\bin');
		end;
		ModPath('CLASSPATH', '.');
	end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
	path : String;
begin
	if CurUninstallStep = usUninstall then begin
		path := ExpandConstant('{app}');
		ModPath('PATH', path+'\bin');
		ModPath('PATH', path+'\etc');
		ModPath('PATH', path+'\3rdparty\xerces-c_2_5_0\bin');
		ModPath('PATH', path+'\3rdparty\xalan-c_1_8_0\bin');
		ModPath('CLASSPATH', '.');
	end;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
var
  MyComponents : String;
  MyCompLen : Integer;
begin
	if CurPageID = wpWelcome then begin
		if (InstallVS2008 = False) and (InstallVS2005 = False) then begin
			VSOptionPage := CreateInputOptionPage(wpWelcome,'Visual Studio Information', '',
			'The installer cannot find either Visual Studio 2005 or Visual Studio 2008, please select which version of UDM you want to install below, then click Next.',
			True, False);
			VSOptionPage.Add('UDM compatible with Visual Studio 2005');
			VSOptionPage.Add('UDM compatible with Visual Studio 2008');
			VSOptionPage.Values[0] := True;
		end;
	end;

	if CurPageID = wpSelectComponents then begin
		MyComponents := WizardSelectedComponents(false);
		MyCompLen := length(MyComponents);
		if MyComponents[MyCompLen] = 'm' then begin
			InstallSystemWide := true;
		end else begin
			InstallSystemWide := false;
		end;
	end;

	Result := True;
end;
